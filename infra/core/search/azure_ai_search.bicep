targetScope = 'resourceGroup'

@description('Tags that will be applied to all resources')
param tags object = {}

@description('Azure Search resource name')
param resourceName string

@description('Azure Search SKU name')
param azureSearchSkuName string = 'standard'

@description('Azure storage account resource ID')
param storageAccountResourceId string

@description('container name')
param containerName string = 'knowledgebase'

@description('AI Services account name for the project parent')
param aiServicesAccountName string = ''

@description('AI project name for creating the connection')
param aiProjectName string = ''

@description('Id of the user or app to assign application roles')
param principalId string

@description('Principal type of user or app')
param principalType string

@description('Name for the AI Foundry search connection')
param connectionName string = 'azure-ai-search-connection'

@description('Knowledge base name for the Foundry IQ MCP connection')
param knowledgeBaseName string = 'zava-company-kb'

@description('Name for the KB MCP project connection')
param kbMcpConnectionName string = 'kb-mcp-connection'

@description('Location for all resources')
param location string = resourceGroup().location

// Get reference to the AI Services account and project to access their managed identities
resource aiAccount 'Microsoft.CognitiveServices/accounts@2025-04-01-preview' existing = if (!empty(aiServicesAccountName) && !empty(aiProjectName)) {
  name: aiServicesAccountName

  resource aiProject 'projects' existing = {
    name: aiProjectName
  }
}

// Azure Search Service
resource searchService 'Microsoft.Search/searchServices@2024-06-01-preview' = {
  name: resourceName
  location: location
  tags: tags
  sku: {
    name: azureSearchSkuName
  }
  identity: {
    type: 'SystemAssigned'
  }
  properties: {
    replicaCount: 1
    partitionCount: 1
    hostingMode: 'default'
    authOptions: {
      aadOrApiKey: {
        aadAuthFailureMode: 'http401WithBearerChallenge'
      }
    }
    disableLocalAuth: false
    encryptionWithCmk: {
      enforcement: 'Unspecified'
    }
    publicNetworkAccess: 'enabled'
  }
}

// Reference to existing Storage Account
resource storageAccount 'Microsoft.Storage/storageAccounts@2023-05-01' existing = {
  name: last(split(storageAccountResourceId, '/'))
}

// Reference to existing Blob Service
resource blobService 'Microsoft.Storage/storageAccounts/blobServices@2023-05-01' existing = {
  parent: storageAccount
  name: 'default'
}

// Storage Container (create if it doesn't exist)
resource storageContainer 'Microsoft.Storage/storageAccounts/blobServices/containers@2023-05-01' = {
  parent: blobService
  name: containerName
  properties: {
    publicAccess: 'None'
  }
}

// RBAC Assignments

// Search needs to read from Storage
resource searchToStorageRoleAssignment 'Microsoft.Authorization/roleAssignments@2022-04-01' = {
  name: guid(storageAccount.id, searchService.id, 'Storage Blob Data Reader', uniqueString(deployment().name))
  scope: storageAccount
  properties: {
    roleDefinitionId: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', '2a2b9908-6ea1-4ae2-8e65-a410df84e7d1') // Storage Blob Data Reader
    principalId: searchService.identity.principalId
    principalType: 'ServicePrincipal'
  }
}

// Search needs OpenAI access (AI Services account)
resource searchToAIServicesRoleAssignment 'Microsoft.Authorization/roleAssignments@2022-04-01' = if (!empty(aiServicesAccountName)) {
  name: guid(aiServicesAccountName, searchService.id, 'Cognitive Services OpenAI User', uniqueString(deployment().name))
  scope: aiAccount
  properties: {
    roleDefinitionId: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', '5e0bd9bd-7b93-4f28-af87-19fc36ad61bd') // Cognitive Services OpenAI User
    principalId: searchService.identity.principalId
    principalType: 'ServicePrincipal'
  }
}

// AI Project needs Search access - Service Contributor
resource aiServicesToSearchServiceRoleAssignment 'Microsoft.Authorization/roleAssignments@2022-04-01' = if (!empty(aiServicesAccountName) && !empty(aiProjectName)) {
  name: guid(searchService.id, aiServicesAccountName, aiProjectName, 'Search Service Contributor', uniqueString(deployment().name))
  scope: searchService
  properties: {
    roleDefinitionId: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', '7ca78c08-252a-4471-8644-bb5ff32d4ba0') // Search Service Contributor
    principalId: aiAccount::aiProject!.identity.principalId
    principalType: 'ServicePrincipal'
  }
}

// AI Project needs Search access - Index Data Contributor
resource aiServicesToSearchDataRoleAssignment 'Microsoft.Authorization/roleAssignments@2022-04-01' = if (!empty(aiServicesAccountName) && !empty(aiProjectName)) {
  name: guid(searchService.id, aiServicesAccountName, aiProjectName, 'Search Index Data Contributor', uniqueString(deployment().name))
  scope: searchService
  properties: {
    roleDefinitionId: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', '8ebe5a00-799e-43f5-93ac-243d3dce84a7') // Search Index Data Contributor
    principalId: aiAccount::aiProject!.identity.principalId
    principalType: 'ServicePrincipal'
  }
}

// User permissions - Search Index Data Contributor
resource userToSearchRoleAssignment 'Microsoft.Authorization/roleAssignments@2022-04-01' = {
  name: guid(searchService.id, principalId, 'Search Index Data Contributor', uniqueString(deployment().name))
  scope: searchService
  properties: {
    roleDefinitionId: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', '8ebe5a00-799e-43f5-93ac-243d3dce84a7') // Search Index Data Contributor
    principalId: principalId
    principalType: principalType
  }
}

// User permissions - Search Service Contributor (needed for index create/update keyless)
resource userToSearchServiceContributorRoleAssignment 'Microsoft.Authorization/roleAssignments@2022-04-01' = {
  name: guid(searchService.id, principalId, 'Search Service Contributor', uniqueString(deployment().name))
  scope: searchService
  properties: {
    roleDefinitionId: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', '7ca78c08-252a-4471-8644-bb5ff32d4ba0') // Search Service Contributor
    principalId: principalId
    principalType: principalType
  }
}

// Create the AI Search connection using the centralized connection module
module aiSearchConnection '../ai/connection.bicep' = if (!empty(aiServicesAccountName) && !empty(aiProjectName)) {
  name: 'ai-search-connection-creation'
  params: {
    aiServicesAccountName: aiServicesAccountName
    aiProjectName: aiProjectName
    connectionConfig: {
      name: connectionName
      category: 'CognitiveSearch'
      target: 'https://${searchService.name}.search.windows.net'
      authType: 'AAD'
      isSharedToAll: true
      metadata: {
        ApiVersion: '2024-07-01'
        ResourceId: searchService.id
        ApiType: 'Azure'
        type: 'azure_ai_search'
      }
    }
  }
  dependsOn: [
    aiServicesToSearchDataRoleAssignment
  ]
}

// Foundry IQ MCP connection — allows the Foundry Toolbox to call the KB MCP endpoint
// using the project's managed identity for authentication against Azure AI Search.
resource kbMcpConnection 'Microsoft.CognitiveServices/accounts/projects/connections@2026-03-01' = if (!empty(aiServicesAccountName) && !empty(aiProjectName)) {
  parent: aiAccount::aiProject
  name: kbMcpConnectionName
  properties: {
    // 'ProjectManagedIdentity' is a valid REST API auth type but not yet in the Bicep type definitions
    #disable-next-line BCP036
    authType: 'ProjectManagedIdentity'
    category: 'RemoteTool'
    target: 'https://${searchService.name}.search.windows.net/knowledgebases/${knowledgeBaseName}/mcp?api-version=2026-05-01-preview'
    isSharedToAll: true
    audience: 'https://search.azure.com/'
    metadata: {
      ApiType: 'Azure'
    }
  }
  dependsOn: [
    aiServicesToSearchDataRoleAssignment
    aiServicesToSearchServiceRoleAssignment
  ]
}

// Outputs
output searchServiceName string = searchService.name
output searchServiceId string = searchService.id
output searchServicePrincipalId string = searchService.identity.principalId
output storageAccountName string = storageAccount.name
output storageAccountId string = storageAccount.id
output containerName string = storageContainer.name
output storageAccountPrincipalId string = storageAccount.identity.principalId
output searchConnectionName string = (!empty(aiServicesAccountName) && !empty(aiProjectName)) ? aiSearchConnection!.outputs.connectionName : ''
output searchConnectionId string = (!empty(aiServicesAccountName) && !empty(aiProjectName)) ? aiSearchConnection!.outputs.connectionId : ''
output kbMcpConnectionName string = (!empty(aiServicesAccountName) && !empty(aiProjectName)) ? kbMcpConnection.name : ''
